// jackson-utils

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.46.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '3.7.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "3.1.0"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.30.0"
}

group   = 'com.arakelian'
version = '4.0.1'


apply from: "core.gradle"

wrapper {
    gradleVersion = '8.1.1'
}

publishing.publications.mavenJava {
    pom {
        name = "Jackson Utils"
        description = "Utilities for reading and writing JSON and XML using Jackson."
        url = "https://github.com/arakelian/jackson-utils"
        
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        
        developers {
            developer {
                id = 'arakelian'
                name = 'Greg Arakelian'
                email = 'greg@arakelian.com'
            }
        }
        
        scm {
            connection = 'scm:git:https://github.com/arakelian/jackson-utils.git'
            developerConnection = 'scm:git:git@github.com:arakelian/jackson-utils.git'
            url = 'https://github.com/arakelian/jackson-utils.git'
        }
    }
}

dependencies {
    processor 'org.immutables:value:2.9.3'

    // annotations
    api 'org.immutables:value-annotations:2.9.3'
    
    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.18.0'

    // use Java 11 compatible versions
    api 'com.arakelian:more-commons:4.0.1'

    // shadow Guava due to frequent downstream incompatibility issues
    api 'com.google.guava:guava:31.1-jre'

    // shadow because we use very few classes from here and don't want to create
    // unnecessary downstream dependencies
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'commons-io:commons-io:2.11.0'
    
    // using Jackson feature set which requires Java 8+
    api 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    api 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.14.2'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.14.2'

    // logging
    testImplementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
    testImplementation 'org.slf4j:jcl-over-slf4j:2.0.7'
    testImplementation 'org.slf4j:jul-to-slf4j:2.0.7'
    api                'org.slf4j:slf4j-api:2.0.7'

    // for unit testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'net.javacrumbs.json-unit:json-unit:2.37.0'
    testImplementation 'com.arakelian:more-commons:4.0.1:tests'
}
